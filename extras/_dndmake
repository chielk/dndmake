#compdef dndmake

typeset -A opt_args
local races alignments

races=(/etc/dndmake/races/*(:t:r:gs/_/-/))  # Default races
if [[ -d ~/.dndmake/races ]]; then          # Custom races
	races=($races ~/.dndmake/races/*(:t:r:gs/_/-/))
fi
races=(${(u)races[@]})                      # make unique

alignments=('LG' 'LN' 'LE' 'NG' 'NN' 'NE' 'CG' 'CN' 'CE')


_arguments \
	'(--help)-h[show this help message and exit]'\
	'(-h)--help[show this help message and exit]'\
	'(--version)-v[print version and exit]'\
	'(-v)--version[print version and exit]'\
	'(-f --female --male)-m[make a male character]'\
	'(-f --female -m)--male[make a male character]'\
	'(-m --male --female)-f[make a female character]'\
	'(-m --male -f)--female[make a female character]'\
	'(-s --short --tall)-t[make a tall character]'\
	'(-s --short -t)--tall[make a tall character]'\
	'(-t --tall --short)-s[make a short character]'\
	'(-t --tall -s)--short[make a short character]'\
	'(-l --light --heavy)-b[make a heavy character]'\
	'(-l --light -b)--heavy[make a heavy character]'\
	'(-b --heavy --light)-l[make a light character]'\
	'(-b --heavy -l)--light[make a light character]'\
	'(--name)-n[character name]'\
	'(-n)--name[character name]'\
	'(-a)--alignment:alignment:($alignments)'\
	'(--alignment)-a:alignment:($alignments)'\
	':race:($races)'&& ret=0
